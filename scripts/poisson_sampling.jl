##
using Distributed
@everywhere using DrWatson
@everywhere @quickactivate :GradientSensing
@everywhere using JLD2


##
@everywhere function produce_data(params)
    produce_or_load(
        datadir("Poisson"), # on local
        # joinpath(ENV["SCRATCH"], "Poisson"), # on cluster
        params;
        prefix = "waitingtimes",
        suffix = "jld2",
        tag = false,
        loadfile = false,
    ) do params
        sample_waitingtimes(params)
    end
    GC.gc()
    return nothing
end

"""
    sample_waitingtimes(params)
Performs spatial sampling of a concentration profile by measuring waiting times
between Poissonian absorption events along a transect towards a spherical source.

The `params` dictionary must have the following keys, which define the
properties of the simulation:
- `R`: source radius
- `C‚Çõ`: excess concentration at the surface of the source
- `C‚ÇÄ`: background concentration (at infinite distance from the source)
- `Dc`: diffusivity of the attractant molecules
- `U`: speed at which the sensor moves along the transect
- `T`: sensory integration timescale of the sensor
- `Œît`: minimal temporal resolution at which the sensor can distinguish two events
- `N`: number of repetitions for each sampling transect

For any given set of parameters, a grid is generated which starts away from
the source and moves towards it in steps of size `Œîx=U*T`.
Within any spatial interval `Œîx`, the sensor resolves a number of absorption
events which is determined by the local concentration field arising from the source.
Measurements between intervals are independent (i.e. the state of the sensor is
reset in every spatial interval).
Each repetition is stored separately.

The data is saved to a jld2 file containing the objects `waitingtimes` and `r`.

`r`: the midpoints of the spatial grid which has been sampled by the sensor

`waitingtimes`: a `Vector{Vector{Vector{Quantity{<:Real,Unitful.ùêì}}}}`
containing the waiting times sampled during experiments.
`waitingtimes[i][j]` returns the list of waiting times associated to the `i`-th
repetition of the experiment at position `r[j]`.
"""

@everywhere function sample_waitingtimes(params)
    @unpack R, C‚Çõ, C‚ÇÄ, Dc, U, T, Œît, N = params
    Œîx = U*T |> u"Œºm"
    staggered_grids = [generate_grid(R, Œîx) for _ in 1:N]
    waitingtimes = map(grid -> spatial_counting(grid, R, C‚Çõ, C‚ÇÄ, Dc, U, Œît), staggered_grids)
    r = map(midpoints, staggered_grids)
    @strdict waitingtimes r
end

"""
    generate_grid(R, Œîx)
Generate a regular grid of spacing `Œîx` starting at
`max(100u"Œºm", 20R) ¬± Œ¥` and ending at `R ¬± Œ¥`,
where `Œ¥` is a random value in `(-Œîx/2,+Œîx/2)`.
"""

@everywhere function generate_grid(R, Œîx)
    Œ¥ = (rand() - 1/2) * Œîx
    x_end = R + Œ¥
    transect_length = max(100u"Œºm", 20R)
    x_start = x_end + transect_length
    return range(x_start, x_end, step=-Œîx)
end


"""
    spatial_counting(r, R, C‚Çõ, C‚ÇÄ, Dc, U, Œît)
Simulates a concentration sensor moving with speed `U` along a grid `r`.
while counting Poissonian absorption events from the surrounding concentration field.
The concentration field is produced by a spherical source with radius `R`
and excess surface concentration `C‚Çõ` whose center is located at the origin (x=0).

Returns a vector of waiting times between successive counting events for each
interval that makes up the grid `r`.
Measurements are independent between intervals.

**Arguments**
- `r: spatial grid with mesh size equal to the spatial resolution of the sensor; must be sorted in reverse order (`r[1]>r[end]`) 
- `R`: source radius
- `C‚Çõ`: excess concentration at the surface of the source
- `C‚ÇÄ`: backgrund concentration (at infinite distance from the source)
- `Dc`: diffusivity of the attractant molecules
- `U`: velocity with which the sensor moves through the interval
- `Œît`: minimal temporal resolution at which the sensor can distinguish two signals
"""

@everywhere function spatial_counting(r, R, C‚Çõ, C‚ÇÄ, Dc, U, Œît)
    map(k -> spatial_counting(r[k], r[k-1], R, C‚Çõ, C‚ÇÄ, Dc, U, Œît), eachindex(r)[2:end])
end

"""
    spatial_counting(x‚ÇÄ, x‚ÇÅ, R, C‚Çõ, C‚ÇÄ, Dc, U, Œît)
Simulates a concentration sensor moving with speed `U` in an interval [x‚ÇÄ,x‚ÇÅ]
while counting Poissonian absorption events from the surrounding concentration field.
The concentration field is produced by a spherical source with radius `R`
and excess surface concentration `C‚Çõ` whose center is located at the origin (x=0).

Returns a vector of waiting times between successive counting events.

**Arguments**
- `x‚ÇÄ`: initial position
- `x‚ÇÅ`: final position (`x‚ÇÅ>x‚ÇÄ`)
- `R`: source radius
- `C‚Çõ`: excess concentration at the surface of the source
- `C‚ÇÄ`: backgrund concentration (at infinite distance from the source)
- `Dc`: diffusivity of the attractant molecules
- `U`: velocity with which the sensor moves through the interval
- `Œît`: minimal temporal resolution at which the sensor can distinguish two signals
"""

@everywhere function spatial_counting(x‚ÇÄ, x‚ÇÅ, R, C‚Çõ, C‚ÇÄ, Dc, U, Œît)
    event_times = zero([Œît])
    # use expected counts at midpoint to sizehint event_times
    xp = (x‚ÇÄ+x‚ÇÅ)/2
    n = nevents(C(xp,R,C‚Çõ,C‚ÇÄ), Œît, Dc)
    sizehint!(event_times, 2*ceil(Int,n))
    # start from x‚ÇÄ and move in steps U*Œît
    i = 0
    x = x‚ÇÄ
    while x < x‚ÇÅ
        i += 1
        x += U*Œît
        y = abs(x) < R ? R : x # equivalent to getting stuck at the surface
        œâ = eventrate(C(y,R,C‚Çõ,C‚ÇÄ), Dc)
        if rand() < upreferred(œâ*Œît)
            # if an event occurs, record its timing
            push!(event_times, i*Œît)
        end
    end
    diff(event_times)
end


## parameters
f = jldopen(datadir("Poisson", "RC.jld2"))
R, C‚Çõ = f["R"], f["C‚Çõ"]
close(f)
# parameters for the sensing process
T = [50, 100, 200]u"ms" # sensory integration timescale
Dc = [500]u"Œºm^2/s" # molecular diffusivity of compound
U = [50]u"Œºm/s" # speed
C‚ÇÄ = [1]u"nM" # background attractant concentration
Œît = [1e-4]u"ms" # temporal resolution of the simulation
N = [250] # number of repetitions for each sampling

allparams = @strdict R C‚Çõ T Dc U C‚ÇÄ Œît N
dicts = dict_list(allparams)

## run
pmap(produce_data, dicts)
